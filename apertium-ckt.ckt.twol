Alphabet

 А Б В Г Д Е Ё Ж З И Й К Ӄ Л Ԓ М Н Ӈ О П Р С Т У Ф Х Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
 а б в г д е ё ж з и й к ӄ л ԓ м н ӈ о п р с т у ф х ц ч ш щ ъ ы ь э ю я

 %{T%}:т %{T%}:0
 %{E%}:э %{T%}:а 
 %{N%}:ӈ %{N%}:г

 %{к%}:к %{к%}:г 
 %{е%}:е %{е%}:0 
 %{ы%}:ы %{ы%}:0

 %{¶%}:0          ! Dominant vowel harmony

 %»:0           ! Incorporation boundary
 %>:0           ! Morpheme boundary
  ;

Sets

Vow   = А Е Ё И О У Ы Э Ю Я
        а е ё и о у ы э ю я ;

Cns   = Б В Г Д Ж З Й К Ӄ Л Ԓ М Н Ӈ П Р С Т Ф Х Ц Ч Ш Щ Ъ Ь
        б в г д ж з й к ӄ л ԓ м н ӈ п р с т ф х ц ч ш щ ъ ь ;

Coronal = Т Л Й Ч Р
          т л й ч р ;

Rules 

"Ergative stem allomorphy after consonant"
%{T%}:0 <=> :Cns %>: _ ;

"Ergative stem allomorph after vowel"
%{T%}:т <=> :Vow %>: _ ;

"Vowel harmony for E (dominant)"
%{E%}:а <=> [ а: | о: | %{¶%}: ] :* _ ; 
                                  _ :* [ а: | о: | %{¶%}: ] :* ; 

"Vowel harmony for E"
%{E%}:э <=>                            _ ; 
            except 
               [ а: | о: | %{¶%}: ] :* _ ; 
                                       _ :* [ а: | о: | %{¶%}: ] :* ; 

"Vowel harmony for non-archiphonemic vowels (dominant)"
Vx:Vy <=> [ а: | о: | %{¶%}: ] :* _ ; 
                                  _ :* [ а: | о: | %{¶%}: ] ; 
        where 
            Vx in ( у ) 
            Vy in ( о ) ;


"Vowel coronal singulative allomorph with -г"
%{N%}:г <=> [ :Vow | :Cns - :Coronal ] %>: %{ы%}: л (:0) _ ; 

! у у н ъ > {ы} л {ы} {N} > {ы} н
! о о н ъ 0  ы  л  0   г  0  ы  н

"Vowel coronal singulative allomorph with -ӈ"
%{N%}:ӈ <=>  :Coronal %>: _ ; 
         except 
          [ :Vow | :Cns - :Coronal ] %>: %{ы%}: л (:0) _ ; 

"Consonant final schwa epenthesis"
%{ы%}:ы <=>     [ :Cns - :Coronal ] (:0) %>: _  ; 
                           %>: :Coronal _ ; 

! Vowel >   л г
! Cns   > ы л г
! Cor   > л ы ӈ 

! у:о у:о н:н ъ:ъ >:0 {ы}:ы л:л {ы}:0 {N}:г {¶}:0 >:0 {ы}:ы н

